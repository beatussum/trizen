# Maintainer: Trizen <echo dHJpemVuQHByb3Rvbm1haWwuY29tCg== | base64 -d>

_pkgname=trizen
pkgname=trizen-git
pkgver=1.58.13
pkgrel=1
pkgdesc="Trizen AUR Package Manager: lightweight pacman wrapper and AUR helper (-git version)."
arch=('any')
url="https://github.com/trizen/${_pkgname}"
license=('GPL3')

provides=('trizen')
conflicts=('trizen')

depends=(
         'git'
         'pacutils'
         'perl>=5.20.0'
         'perl-libintl-perl'
         'perl-list-moreutils'
         'perl-libwww'
         'perl-term-ui'
         'pacman'
         'perl-json'
         'perl-data-dump'
         'perl-lwp-protocol-https'
         'perl-term-readline-gnu'
        )

optdepends=(
            'highlight: for syntax highlighting'
            'perl-json-xs: faster JSON deserialization'
            #'perl-term-readline-gnu: better STDIN support'
           )

source=("git+https://github.com/trizen/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    git describe --always | sed -e 's|-|.|g'
}

package() {
  cd "$_pkgname"
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
}
